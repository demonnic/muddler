/*
 * This Groovy source file was generated by the Gradle 'init' task.
 */
package muddler
import groovy.json.JsonSlurper
import groovy.xml.StreamingMarkupBuilder
import groovy.xml.XmlUtil
import static groovy.io.FileType.File
import muddler.mudlet.packages.*
import java.util.regex.Pattern
import java.time.ZonedDateTime
import java.time.format.DateTimeFormatter


class App {
  static void main(String[] args) {
    // read mfile and setup packageName and packageVersion
    def mfile = new File('./mfile')
    def readme = new File('./README.md')
    def packageName = ""
    def packageVersion = ""
    def packageAuthor = ""
    def packageTitle = ""
    def packageDesc = ""
    def packageIcon = ""
    def now = ZonedDateTime.now()
    def dtf = DateTimeFormatter.ofPattern("yyyy-MM-dd'T'HH:mm:ssZ")
    def packageTimestamp = dtf.format(now)
    //def packageTimestamp = now.format("yyyy-MM-dd'T'HH:mm:ss'Z'")
    if (readme.exists()) {
      packageDesc = readme.text
    }
    if (mfile.exists()) {
      def config = new JsonSlurper().parse(mfile)
      packageName = config.package ?: packageName
      packageVersion = config.version ?: packageVersion
      packageAuthor = config.author ?: packageAuthor
      packageTitle = config.title ?: packageTitle
      packageDesc = config.description ?: packageDesc
      packageIcon = config.icon ?: packageIcon
    }
    if (packageName == "") {
      def fullPath = System.properties['user.dir']
      packageName = fullPath.split(Pattern.quote(File.separator))[-1]
    }
    // we will leverage Ant for token filtering and zip creation
    def ant = new AntBuilder()
    def outputDir = new File('build')
    // all builds are clean builds. fight me.
    outputDir.deleteDir()
    def tmp = new File(outputDir, 'tmp')
    tmp.mkdirs()
    // filter our source files from src into build/filtered/src and replace @PKGNAME@ with the package name as used by Mudlet
    // no more images failing to load because the package name changed or you bumped version
    ant.copy(todir:'build/filtered/src') {
      fileset(dir: "src/") {
        exclude(name: "resources/")
      }
      filterset(){
        filter(token: "PKGNAME", value: packageName)
        filter(token: "VERSION", value: packageVersion)
      }
    }
    
    // now create the individual item type packages
    def aliasP = new AliasPackage()
    def scriptP = new ScriptPackage()
    def timerP = new TimerPackage()
    def triggerP = new TriggerPackage()
    def keyP = new KeyPackage()
    def builder = new StreamingMarkupBuilder()
    builder.encoding = 'UTF-8'
    def mudletPackage = builder.bind {
      mkp.xmlDeclaration()
      mkp.yieldUnescaped '<!DOCTYPE MudletPackage>'
      'MudletPackage'(version: "1.001") {
        mkp.yieldUnescaped scriptP.toXML()
        mkp.yieldUnescaped aliasP.toXML()
        mkp.yieldUnescaped timerP.toXML()
        mkp.yieldUnescaped triggerP.toXML()
        mkp.yieldUnescaped keyP.toXML()
      }
    }
    def mpXML = XmlUtil.serialize(mudletPackage)
    
    new File(outputDir,packageName + ".xml").withWriter { writer ->
      writer.write(mpXML)
    }

    def configLua = "mpackage = [[$packageName]]\n"
        if (! packageAuthor.isEmpty()) {
      configLua += "author = [[$packageAuthor]]\n"
    }
    def validIcon = false
    if (! packageIcon.isEmpty()) {
      def iconFile = new File("src${File.separator}resources${File.separator}$packageIcon")
      if (iconFile.exists()) {
        configLua += "icon = [[$packageIcon]]\n"
        validIcon = true
      }
    }
    if (! packageTitle.isEmpty()) {
      configLua += "title = [[$packageTitle]]\n"
    }
    if (! packageDesc.isEmpty()) {
      configLua += "description = [[$packageDesc]]\n"
    }
    if (! packageVersion.isEmpty()) {
      configLua += "version = [[$packageVersion]]\n"
    }
    configLua += "created = [[$packageTimestamp]]\n"
    new File(tmp, 'config.lua').withWriter { writer ->
      writer.write(configLua)
    }

    def resDir = new File("src${File.separator}resources")
    if (resDir.exists()) {
      ant.copy(toDir: 'build/tmp') {
        fileset(dir: 'src/resources')
      }
    }
    if (validIcon) {
      def iconDir = new File(tmp, '.mudlet/Icon')
      iconDir.mkdirs()
      ant.copy(file: "build/tmp/$packageIcon", tofile: "build/tmp/.mudlet/Icon/$packageIcon")
    }

    ant.copy(toDir: 'build/tmp') {
      fileset(file: "build/$packageName" + ".xml")
    }
    ant.zip(baseDir: 'build/tmp', destFile: "build/$packageName" + ".mpackage")
  }
}
