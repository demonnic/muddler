plugins {
  id 'groovy'
  id 'application'

  // Shadow jar plugin for creating the uberjar    
  id 'com.github.johnrengelman.shadow' version '7.0.0'

}

mainClassName = "muddler.App"

repositories {
  jcenter()
}

dependencies {
  implementation 'org.apache.groovy:groovy-all:4.0.3'
}

// this feels like a lot to not repeat the version, but by Jove I'm doing it anyway!
task generateGroovy(type: Copy) {
  def templateContext = [version: project.version]
  inputs.properties templateContext // for gradle up-to-date check
  from "src/template/groovy/"
  into "$buildDir/generated/groovy"
  expand templateContext
}
// add the generated files to the srcDir and ensure they're generated before compileGroovy
sourceSets.main.groovy.srcDir "$buildDir/generated/groovy"
compileGroovy.dependsOn generateGroovy

task dockerImage(type:Exec, dependsOn: ['clean', 'shadowJar']) {
  commandLine 'docker', 'build', '-t', "demonnic/muddler:$version", '.'
}

task dockerTest(type:Exec, dependsOn: ['clean', 'shadowJar']) {
  commandLine 'docker', 'build', '-t', "demonnic/muddler:test", '.'
}

task dockerLatest(type:Exec, dependsOn: ['clean', 'shadowJar']) {
  commandLine 'docker', 'build', '-t', "demonnic/muddler:latest", '.'
}

task mpackage(type:Exec, dependsOn: ['clean', 'shadowJar']) {
  def jar = "../build/libs/muddle-$version-all.jar"
  workingDir = "./mpackage"
  commandLine 'java', '-jar', jar
}

task muddle(dependsOn: ['clean', 'shadowDistZip', 'shadowDistTar', 'dockerImage', 'dockerLatest', 'dockerTest', 'mpackage'])
